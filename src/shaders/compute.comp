#version 450
#extension GL_ARB_separate_shader_objects : enable

#define WORKGROUP_SIZE 32
layout(local_size_x = WORKGROUP_SIZE, local_size_y = 1, local_size_z = 1) in;

layout(set = 0, binding = 0) uniform CameraBufferObject {
    mat4 view;
    mat4 proj;
} camera;

layout(set = 1, binding = 0) uniform Time {
    float deltaTime;
    float totalTime;
};

struct Blade {
    vec4 v0;
    vec4 v1;
    vec4 v2;
    vec4 up;
};

// TODO: Add bindings to:
// 1. Store the input blades
// 2. Write out the culled blades
// 3. Write the total number of blades remaining

// The project is using vkCmdDrawIndirect to use a buffer as the arguments for a draw call
// This is sort of an advanced feature so we've showed you what this buffer should look like
//
 layout(set = 2, binding = 2) buffer NumBlades {
 	  uint vertexCount;   // Write the number of blades remaining here
 	  uint instanceCount; // = 1
 	  uint firstVertex;   // = 0
 	  uint firstInstance; // = 0
 } numBlades;

 layout(set = 2, binding = 0) buffer Blades {
	Blade blades[];
 };

 layout(set = 2, binding = 1) buffer CulledBlades {
	Blade culledBlades[];
 };

bool inBounds(float value, float bounds) {
    return (value >= -bounds) && (value <= bounds);
}

void main() {
	// Reset the number of blades to 0
	if (gl_GlobalInvocationID.x == 0) {
		 numBlades.vertexCount = 0;
	}

	uint idx = gl_GlobalInvocationID.x;
	barrier(); // Wait till all threads reach this point
	Blade blade = blades[idx];

    // TODO: Apply forces on every blade and update the vertices in the buffer
	vec3 v0 = blade.v0.xyz;
	vec3 v1 = blade.v1.xyz;
	vec3 v2 = blade.v2.xyz;
	vec3 up = blade.up.xyz;

	float theta = blade.v0.w;
	float height = blade.v1.w;
	float width = blade.v2.w;
	float stiffness = blade.up.w;

	// Gravity
	float g = 9.8;
	vec3 dir = vec3(0.0, -1.0, 0.0);
	vec3 gE = dir * g;
	vec3 f = normalize(cross(up, vec3(sin(theta), 0.0, cos(theta))));
	vec3 gF = 0.25 * length(gE) * f;
	vec3 gravity = gE + gF;

	// Recovery
	vec3 iv2 = v0 + height * up;
	vec3 recovery =  (iv2 - v2) * stiffness;

	// Wind
	vec3 windDir = normalize(vec3(1.0, 0.0, -1.0));
	float amp = cos((dot(vec3(v0.x, 0.0, v0.z), windDir) + totalTime * 2.0));
	vec3 wind = windDir * v0 * amp; 

	// total force
	vec3 totalF = (gravity + recovery + wind) * deltaTime;

	// state validation
	v2 += totalF;
	v2 = v2 - up * min(dot(up, v2 - v0), 0.0);

	float lproj = length(v2 - v0 - up * dot( v2 - v1, up));
	v1 = v0 + height * up * max(1 - (lproj / height), 0.05 * max(lproj / height, 1));

	float L0 = length(v2 - v0);
	float L1 = length(v2 - v1) + length(v1 - v0);
	float n = 3.0;
	float L = (2.0 * L0 + (n - 1.0) * L1) / (n + 1.0);
	float r = height / L;

	v1 = v0 + r * (v1 - v0);
	v2 = v1 + r * (v2 - v1);

	blade.v1.xyz = v1;
	blade.v2.xyz = v2;
	blades[idx] = blade;

	// TODO: Cull blades that are too far away or not in the camera frustum and write them
	// to the culled blades buffer
	// Note: to do this, you will need to use an atomic operation to read and update numBlades.vertexCount
	// You want to write the visible blades to the buffer without write conflicts between threads

	bool culled = false;

	//Orientation culling
	float threshold = 0.8;
	mat4 invView = inverse(camera.view);
	vec3 viewVec = (invView * vec4(0.0,0.0,1.0,0.0)).xyz;
	if(abs(dot(f,viewVec)) > threshold) {
		culled = true;
	}
	

	
	//View-frustum culling
	if(!culled) {
		vec3 m = 0.25 * v0 + 0.5 * v1 + 0.25 * v2;

		mat4 VPMatrix = camera.proj * camera.view;
		vec4 v0_ndc = VPMatrix * vec4(v0, 1.0);
		vec4 m_ndc = VPMatrix * vec4(m, 1.0);
		vec4 v2_ndc = VPMatrix * vec4(v2, 1.0);

		float tolerance = 0.1;
		float h_v0 = v0_ndc.w + tolerance;
		float h_m = m_ndc.w + tolerance;
		float h_v2 = v2_ndc.w + tolerance;

		if (!(inBounds(v0_ndc.x, h_v0) && inBounds(v0_ndc.y, h_v0) && inBounds(v0_ndc.z, h_v0)
		&& inBounds(m_ndc.x, h_m) && inBounds(m_ndc.y, h_m) && inBounds(m_ndc.z, h_m)
		&& inBounds(v2_ndc.x, h_v2) && inBounds(v2_ndc.y, h_v2) && inBounds(v2_ndc.z, h_v2))) {
			culled = true;
		}
	}
	
	//Distance culling
	if(!culled) {
		float maxDist = 30.0;
		uint numBucks = 5;

		vec3 v0_view = (camera.view * vec4(v0, 1.0)).xyz;
		vec3 up_view = normalize((camera.view * vec4(up, 0.0)).xyz);
		float dist_ndc = length(v0_view - dot(v0_view , up_view) * up_view);
		if((idx % numBucks) > numBucks * (1.0 - dist_ndc / maxDist)) {
			culled = true;
		}
	}
	

	if(!culled) {
		culledBlades[atomicAdd(numBlades.vertexCount, 1)] = blade;
	}
	

}
